# This patch is made by RehabMan.
# The related thread:
# http://www.tonymacx86.com/hp-probook-mavericks/118805-full-range-brightness-using-acpibacklight-31.html#post878535

into method label _INI parent_label PCI0 code_regex If\s+\(_OSI\s+\(\"Windows\s2012\"\)\) replace_matched
begin If(LOr(_OSI("Darwin"),_OSI("Windows 2012"))) end;

into device label PNLF remove_entry;
into definitionblock code_regex . insert
begin
Scope (\_SB)\n
{\n
    Device (PNLF)\n
    {\n
        // normal PNLF declares (note some of this probably not necessary)\n
        Name (_HID, EisaId ("APP0002"))\n
        Name (_CID, "backlight")\n
        Name (_UID, 10)\n
        Name (_STA, 0x0B)\n
        // _BCM/_BQC: set/get for brightness level\n
        Method (_BCM, 1, NotSerialized)\n
        {\n
            // Update backlight via existing DSDT methods\n
            ^^PCI0.IGPU.DD02._BCM(Arg0)\n
        }\n
        Method (_BQC, 0, NotSerialized)\n
        {\n
            Return(\_SB.PCI0.LPCB.EC0.BRTS)\n
        }\n
        Method (_DOS, 1, NotSerialized)\n
        {\n
            ^^PCI0.IGPU._DOS(Arg0)\n
        }\n
        Method (_BCL, 0, NotSerialized)\n
        {\n
            Return(^^PCI0.IGPU.DD02._BCL())\n
        }\n
        // extended _BCM/_BQC for setting "in between" levels\n
        Method (XBCM, 1, NotSerialized)\n
        {\n
            // Update backlight via existing DSDT methods\n
            ^^PCI0.IGPU.DD02._BCM(Arg0)\n
        }\n
        Method (XBQC, 0, NotSerialized)\n
        {\n
            Return(\_SB.PCI0.LPCB.EC0.BRTS)\n
        }\n
        // Use XOPT=1 to disable smooth transitions\n
        Name (XOPT, Zero)\n
        // XRGL/XRGH: defines the valid range\n
        Method (XRGL, 0, NotSerialized)\n
        {\n
            Store(_BCL(), Local0)\n
            Store(DerefOf(Index(Local0, 2)), Local0)\n
            Return(Local0)\n
        }\n
        Method (XRGH, 0, NotSerialized)\n
        {\n
            Store(_BCL(), Local0)\n
            Store(DerefOf(Index(Local0, Subtract(SizeOf(Local0), 1))), Local0)\n
            Return(Local0)\n
        }\n
    }\n
}\n
end;
